
apply plugin: "groovy"


sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {

    compile('org.rundeck:rundeck-core:2.6.11')
    compile('org.codehaus.groovy:groovy-all:2.4.7')

    pluginLibs('org.apache.maven:maven-repository-metadata:3.3.9')
    pluginLibs('commons-codec:commons-codec:1.9')
    pluginLibs('org.codehaus.groovy.modules.http-builder:http-builder:0.7')

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testRuntime('cglib:cglib-nodep:3.1')
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"

    def libList = configurations.pluginLibs.collect { "lib/" + it.name }.join(" ")

    manifest {
        attributes(
                "Rundeck-Plugin-Version": "1.1",
                "Rundeck-Plugin-File-Version": version,
                "Rundeck-Plugin-Archive": "true",
                "Rundeck-Plugin-Classnames": "de.bitbordell.rundeck.plugin.maven.repository.ArtifactDeliveryStepPlugin",
                "Rundeck-Plugin-Libs": "${libList}"
        )
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
